package hospitalselectionrules;

import com.wordpress.salaboy.model.Emergency;
import com.wordpress.salaboy.model.Call;
import com.wordpress.salaboy.model.Patient;
import com.wordpress.salaboy.model.Hospital;
import com.wordpress.salaboy.model.Ambulance;

import com.wordpress.salaboy.acc.HospitalDistanceCalculationData;

import org.drools.runtime.process.WorkflowProcessInstance;

import java.util.Map;
import com.wordpress.salaboy.messaging.*;
import com.wordpress.salaboy.model.messages.*;
import com.wordpress.salaboy.model.serviceclient.*;
import com.wordpress.salaboy.context.tracking.*;
import com.wordpress.salaboy.model.serviceclient.PersistenceServiceProvider.PersistenceServiceType;

import accumulate com.wordpress.salaboy.acc.HospitalDistanceCalculator hospitalDistanceCalculator;

query "getPatient"
    //@TODO add more filters per process/emergency
    patient: Patient()
end

rule "Load Emergency"
    salience 20
    ruleflow-group "hospital-selection"
    when
       $processInstance: WorkflowProcessInstance() 
    then
        Emergency emergency = (Emergency)$processInstance.getVariable("emergency");
        insert(emergency);
end

rule "Rank Hospital Speciality that matches EmergencyType"
    
    ruleflow-group "hospital-selection"
    when
        $processInstance: WorkflowProcessInstance()
        $emergency: Emergency($type: type.name)
        $selectedHospital: Hospital() from accumulate ($hospital: Hospital() 
                                                        from PersistenceServiceProvider.getPersistenceService().getAllHospitals(), 
                hospitalDistanceCalculator(new HospitalDistanceCalculationData($hospital, $emergency))

        )
      

    then
        
        String callId = ((Call)$processInstance.getVariable("call")).getId();
        //Send Hospital Selected Message
        MessageFactory.sendMessage(new HospitalSelectedMessage(callId, $selectedHospital));
        
end